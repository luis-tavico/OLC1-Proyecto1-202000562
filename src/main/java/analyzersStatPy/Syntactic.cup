package analyzersStatPy;

import java_cup.runtime.*;
import java.util.LinkedList;
import errors.SintaxError;

import instructions.Variable;
import instructions.Statement;
import instructions.Operation;

import instructions.Function;
import instructions.Declaration_Assignment;
import instructions.If;
import instructions.ElseIf;
import instructions.Switch;
import instructions.Case;
import instructions.For;
import instructions.While;
import instructions.DoWhile; 
import instructions.Print;
import instructions.SearchValue;

import enums.EnumOperations;
import enums.EnumUnitaryOperations;
import enums.EnumTerminals;

parser code {:

    public LinkedList<Statement> AST;
    public LinkedList<SintaxError> errors = new LinkedList<>();
    public LinkedList<Variable> variables = new LinkedList<>();
    public LinkedList<Variable> variables_json = new LinkedList<>();

    public void setValues (LinkedList<Variable> variablesjson) {
        variables_json = variablesjson;
    }

    public void syntax_error(Symbol s){
        if (s.value != null)
            errors.add(new SintaxError(s.value.toString(), s.left, s.right));
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 

    }

            :}

terminal String VOID, MAIN, IF, ELSEIF, ELSE, SWITCH, CASE, DEFAULT, FOR, WHILE, DO, CONSOLE, WRITE, CONTINUE, BREAK;
terminal String DECIMAL, INTEGER, TEXT, CHARACTER, BOOLEAN, ID;
terminal String INT, DOUBLE, CHAR, BOOL, STRING;
terminal String AND, OR, NOT; 
terminal String PLUS, MINUS, TIMES, DIVIDE;
terminal String LEFT_PARENTHESIS, RIGHT_PARENTHESIS, LEFT_CURLY_BRACKET, RIGHT_CURLY_BRACKET, LEFT_SQUARE_BRACKET, RIGHT_SQUARE_BRACKET,
                LESS_THAN, GREATER_THAN, LESS_EQUAL, GREATER_EQUAL, EQUAL, EQUAL_TO, NOT_EQUAL_TO, PERIOD, COMMA, SEMI_COLON, COLON, INCREASE, DECREASE, DOLLAR;

non terminal ini;

non terminal LinkedList<Statement> statements;
non terminal Statement statement;
non terminal Operation expr;
non terminal LinkedList<Operation> expr_list;
non terminal LinkedList<ElseIf> elseifs;
non terminal LinkedList<Case> cases;

non terminal Operation symbols;
non terminal Operation aritmetic;
non terminal Operation relational;
non terminal Operation logical;
non terminal Operation unitary;

non terminal If if;
non terminal For for;

non terminal Statement function;
non terminal LinkedList<Declaration_Assignment> funcstatements;
non terminal Declaration_Assignment funcstatement;

non terminal Statement mainstatement;
non terminal LinkedList<Statement> mainstatements;

non terminal data_type;

precedence left GREATER_THAN, LESS_THAN, GREATER_EQUAL, LESS_EQUAL, EQUAL_TO, NOT_EQUAL_TO;
precedence left AND, OR;
precedence left INCREASE, DECREASE;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LEFT_PARENTHESIS;

start with ini;

ini ::= VOID MAIN LEFT_PARENTHESIS RIGHT_PARENTHESIS LEFT_CURLY_BRACKET mainstatements:s RIGHT_CURLY_BRACKET {: parser.AST = s; :};

mainstatements ::= mainstatement:s mainstatements:t  {: RESULT=t; RESULT.add(s); :}
                 | mainstatement:s {: RESULT = new LinkedList<Statement>(); RESULT.add(s); :};

mainstatement ::= statement:s {: RESULT = s; :}
                | function:f {: RESULT = f; :};

statements ::= statement:s statements:t  {: RESULT=t; RESULT.addFirst(s); :}
             | statement:s {: RESULT = new LinkedList<Statement>(); RESULT.addFirst(s); :};

statement ::= data_type:d ID:i SEMI_COLON {: RESULT = new Declaration_Assignment(i); :}
            | data_type:d ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); :}
            | ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); :}
            | if:i {: RESULT=i; :}
            | SWITCH LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET {:RESULT = new Switch(e);:}
            | SWITCH LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET cases:c RIGHT_CURLY_BRACKET {:RESULT = new Switch(e, c);:}
            | SWITCH LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET cases:c DEFAULT COLON statements:s RIGHT_CURLY_BRACKET {: RESULT = new Switch(e, c, s); :}
            | for:f {: RESULT=f; :}
            | WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {:RESULT = new While(e, s);:}
            | WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET {:RESULT = new While(e);:}            
            | DO LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS SEMI_COLON {: RESULT = new DoWhile(s, e); :}
            | DO LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS SEMI_COLON {: RESULT = new DoWhile(e); :}
            | CONSOLE PERIOD WRITE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS SEMI_COLON {: RESULT = new Print(e); :}
            | CONTINUE:c SEMI_COLON {: RESULT = new Operation(c, EnumTerminals.CONTINUE); :}
            | BREAK:b SEMI_COLON {: RESULT = new Operation(b, EnumTerminals.BREAK); :}
            | error RIGHT_CURLY_BRACKET
            | error SEMI_COLON;

data_type ::= INT:i {: RESULT = String.valueOf(i); :}
            | DOUBLE:d {: RESULT = String.valueOf(d); :}
            | CHAR:c {: RESULT = String.valueOf(c); :}
            | STRING:s {: RESULT = String.valueOf(s); :}
            | BOOL:b {: RESULT = String.valueOf(b); :};

expr_list ::= expr_list:l COMMA expr:e {: RESULT = l; RESULT.add(e); :}
            | expr:e {: RESULT = new LinkedList<Operation>(); RESULT.add(e); :};

function ::= VOID ID:i LEFT_PARENTHESIS RIGHT_PARENTHESIS LEFT_CURLY_BRACKET funcstatements:f RIGHT_CURLY_BRACKET {: RESULT = new Function(i, f, variables, variables_json); :};

funcstatements ::= funcstatement:f funcstatements:s  {: RESULT = s; RESULT.add(f); :}
                 | funcstatement:f {: RESULT = new LinkedList<Declaration_Assignment>(); RESULT.add(f); :};

funcstatement ::= data_type ID:i SEMI_COLON {: RESULT = new Declaration_Assignment(i); :}
                | data_type ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); variables.add(new Variable(i, e, "")); :}
                | ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); :}
                | data_type LEFT_SQUARE_BRACKET RIGHT_SQUARE_BRACKET ID:i EQUAL LEFT_CURLY_BRACKET expr_list:l RIGHT_CURLY_BRACKET SEMI_COLON {: RESULT = new Declaration_Assignment(i, l); :};

if ::= IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = new If(e, s); :}
     | IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET ELSE LEFT_CURLY_BRACKET statements:t RIGHT_CURLY_BRACKET {: RESULT = new If(e, s, t); :}
     | IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET elseifs:l {: RESULT = new If(e, s, l, null); :}
     | IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET elseifs:l ELSE LEFT_CURLY_BRACKET statements:t RIGHT_CURLY_BRACKET {: RESULT = new If(e, s, l, t); :};

elseifs ::= elseifs:f ELSEIF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = f; RESULT.add(new ElseIf(e, s)); :}
          | ELSEIF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = new LinkedList<ElseIf>(); RESULT.add(new ElseIf(e, s)); :};

cases ::= cases:c CASE expr:e COLON statements:s {: RESULT = c; RESULT.add(new Case(e, s)); :}
        | CASE expr:e COLON statements:s {: RESULT = new LinkedList<Case>(); RESULT.add(new Case(e, s)); :};

for ::= FOR LEFT_PARENTHESIS INT ID:i EQUAL INTEGER:n SEMI_COLON expr:e SEMI_COLON expr:x RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = new For(i, n, e, x, s); :}
      | FOR LEFT_PARENTHESIS INT ID:i EQUAL INTEGER:n SEMI_COLON expr:e SEMI_COLON expr:x RIGHT_PARENTHESIS LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET {: RESULT = new For(i, n, e, x); :};

expr ::= symbols:s {: RESULT = s; :}
       | aritmetic:a {: RESULT = a; :}
       | relational:r {: RESULT = r; :}
       | logical:l {: RESULT = l; :}
       | unitary:u {: RESULT = u; :}
       | LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS {: RESULT = new Operation(e); :};

symbols ::= ID:i {: RESULT = new Operation(i, EnumTerminals.ID); :}
          | TEXT:t {: RESULT = new Operation(t, EnumTerminals.TEXT); :}
          | CHARACTER:c {: RESULT = new Operation(c, EnumTerminals.CHARACTER); :}
          | INTEGER:i {: RESULT = new Operation(i, EnumTerminals.INTEGER); :}
          | DECIMAL:d {: RESULT = new Operation(d, EnumTerminals.DECIMAL); :}
          | BOOLEAN:b {: RESULT = new Operation(b, EnumTerminals.BOOLEAN); :}
          | DOLLAR LEFT_CURLY_BRACKET ID COMMA TEXT:p COMMA TEXT:i RIGHT_CURLY_BRACKET {: SearchValue val = new SearchValue(variables_json, String.valueOf(p), String.valueOf(i)); RESULT = new Operation(val.getValue(), val.getTypeTerminal()); :};

aritmetic ::= expr:a PLUS expr:b {: RESULT = new Operation(a, EnumOperations.PLUS, b); :}
            | expr:a MINUS expr:b {: RESULT = new Operation(a, EnumOperations.MINUS, b); :}
            | expr:a TIMES expr:b {: RESULT = new Operation(a, EnumOperations.TIMES, b); :}
            | expr:a DIVIDE expr:b {: RESULT = new Operation(a, EnumOperations.DIVIDE, b); :};

relational ::= expr:a GREATER_THAN expr:b {: RESULT = new Operation(a, EnumOperations.GREATER_THAN, b); :}
             | expr:a LESS_THAN expr:b {: RESULT = new Operation(a, EnumOperations.LESS_THAN, b); :}
             | expr:a GREATER_EQUAL expr:b {: RESULT = new Operation(a, EnumOperations.GREATER_EQUAL, b); :}
             | expr:a LESS_EQUAL expr:b {: RESULT = new Operation(a, EnumOperations.LESS_EQUAL, b); :}
             | expr:a EQUAL_TO expr:b {: RESULT = new Operation(a, EnumOperations.EQUAL_TO, b); :}
             | expr:a NOT_EQUAL_TO expr:b {: RESULT = new Operation(a, EnumOperations.NOT_EQUAL_TO, b); :};

logical ::= expr:a AND expr:b {: RESULT = new Operation(a, EnumOperations.AND, b); :}
          | expr:a OR expr:b {: RESULT = new Operation(a, EnumOperations.OR, b); :};

unitary ::= MINUS expr:e {: RESULT = new Operation(e, EnumUnitaryOperations.MINUS); :}
          | NOT expr:e {: RESULT = new Operation(e, EnumUnitaryOperations.NOT); :}
          | expr:e INCREASE  {: RESULT = new Operation(e, EnumUnitaryOperations.INCREASE); :}
          | expr:e DECREASE  {: RESULT = new Operation(e, EnumUnitaryOperations.DECREASE); :};