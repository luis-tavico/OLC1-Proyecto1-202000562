package analyzersStatPy;

import java_cup.runtime.*;
import java.util.LinkedList;
import errors.SintaxError;

import instructions.Statement;
import instructions.Operation;
import instructions.Param;

import instructions.Function;
import instructions.Declaration_Assignment;
import instructions.If;
import instructions.ElseIf;
import instructions.Switch;
import instructions.Case;
import instructions.For;
import instructions.While;
import instructions.DoWhile; 
import instructions.Print;
import instructions.Return;

import enums.EnumOperations;
import enums.EnumTerminals;
import enums.EnumUnitaryOperations;

parser code {:

    public LinkedList<Statement> AST;
    public LinkedList<SintaxError> errors = new LinkedList<>();

    public void syntax_error(Symbol s){
        if (s.value != null)
            errors.add(new SintaxError(s.value.toString(), s.left, (s.right-1)));
        System.out.println("Error sintactico en: " + s.value.toString() + ", Linea: " + (s.left) + ", Columna: " + (s.right-1));
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception {
        System.out.println("Error sintactico NR en: " + s.value + ", Linea: " + (s.left+1) + ", Columna: " + (s.right+1));
    }

            :}

terminal String VOID, MAIN, IF, ELSEIF, ELSE, SWITCH, CASE, DEFAULT, FOR, WHILE, DO, CONSOLE, WRITE, CONTINUE, BREAK, RETURN;
terminal String DECIMAL, INTEGER, TEXT, CHARACTER, BOOLEAN, ID;
terminal String INT, DOUBLE, CHAR, BOOL, STRING;
terminal String AND, OR, NOT; 
terminal String PLUS, MINUS, TIMES, DIVIDE;
terminal String LEFT_PARENTHESIS, RIGHT_PARENTHESIS, LEFT_CURLY_BRACKET, RIGHT_CURLY_BRACKET, LEFT_SQUARE_BRACKET, RIGHT_SQUARE_BRACKET,
                LESS_THAN, GREATER_THAN, LESS_EQUAL, GREATER_EQUAL, EQUAL, EQUAL_TO, NOT_EQUAL_TO, PERIOD, COMMA, SEMI_COLON, COLON, INCREASE, DECREASE, DOLLAR;

non terminal ini;

non terminal LinkedList<Statement> statements;
non terminal Statement statement;
non terminal Operation expr;
non terminal LinkedList<Operation> args_list;
non terminal LinkedList<ElseIf> elseifs;
non terminal LinkedList<Case> cases;

non terminal Operation aritmetic;
non terminal Operation logical;
non terminal Operation symbols;
non terminal Operation unitary;

non terminal If if;
non terminal For for;

non terminal Statement function;
non terminal LinkedList<Declaration_Assignment> funcstatements;
non terminal Declaration_Assignment funcstatement;

non terminal Statement mainstatement;
non terminal LinkedList<Statement> mainstatements;

non terminal data_type;

precedence left GREATER_THAN, LESS_THAN, GREATER_EQUAL, LESS_EQUAL, EQUAL_TO, NOT_EQUAL_TO;
precedence left AND, OR;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LEFT_PARENTHESIS;

start with ini;

ini ::= VOID MAIN LEFT_PARENTHESIS RIGHT_PARENTHESIS LEFT_CURLY_BRACKET mainstatements:s RIGHT_CURLY_BRACKET {: parser.AST = s; :};

mainstatements ::= mainstatement:s mainstatements:t  {: RESULT=t; RESULT.add(s); :}
                 | mainstatement:s {: RESULT = new LinkedList<Statement>(); RESULT.add(s); :};

mainstatement ::= statement:s {: RESULT = s; :}
                | function:f {: RESULT = f; :};

statements ::= statement:s statements:t  {: RESULT=t; RESULT.add(s); :}
             | statement:s {: RESULT = new LinkedList<Statement>(); RESULT.add(s); :};

statement ::= data_type ID:i SEMI_COLON {: RESULT = new Declaration_Assignment(i); :}
            | data_type ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); :}
            | data_type LEFT_SQUARE_BRACKET RIGHT_SQUARE_BRACKET ID EQUAL LEFT_CURLY_BRACKET args_list:a RIGHT_CURLY_BRACKET SEMI_COLON
            | data_type ID EQUAL DOLLAR LEFT_CURLY_BRACKET args_list:a RIGHT_CURLY_BRACKET SEMI_COLON
            | ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); :}
            | if:i {: RESULT=i; :}
            | SWITCH LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET {:RESULT = new Switch(e);:}
            | SWITCH LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET cases:c RIGHT_CURLY_BRACKET {:RESULT = new Switch(e, c);:}
            | SWITCH LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET cases:c DEFAULT COLON statements:s RIGHT_CURLY_BRACKET {: RESULT = new Switch(e, c, s); :}
            | for:f {: RESULT=f; :}
            | WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {:RESULT = new While(e, s);:}
            | WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET {:RESULT = new While(e);:}            
            | DO LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS SEMI_COLON {: RESULT = new DoWhile(s, e); :}
            | DO LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET WHILE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS SEMI_COLON {: RESULT = new DoWhile(e); :}
            | CONSOLE PERIOD WRITE LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS SEMI_COLON {: RESULT = new Print(e); :}
            | error RIGHT_CURLY_BRACKET
            | error SEMI_COLON;

data_type ::= INT:i {: RESULT = i; :}
            | DOUBLE:d {: RESULT = d; :}
            | CHAR:c {: RESULT = c; :}
            | STRING:s {: RESULT = s; :}
            | BOOL:b {: RESULT = b; :};

args_list ::= args_list:a COMMA expr:e {:RESULT = a; RESULT.add(e); :}
            | expr:e {: RESULT = new LinkedList<Operation>(); RESULT.add(e); :};

function ::= VOID ID:i LEFT_PARENTHESIS RIGHT_PARENTHESIS LEFT_CURLY_BRACKET funcstatements:f RIGHT_CURLY_BRACKET {: RESULT = new Function(i, f); :};

funcstatements ::= funcstatement:f funcstatements:s  {: RESULT = s; RESULT.add(f); :}
                 | funcstatement:f {: RESULT = new LinkedList<Declaration_Assignment>(); RESULT.add(f); :};

funcstatement ::= data_type ID:i SEMI_COLON {: RESULT = new Declaration_Assignment(i); :}
                | data_type ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); :}
                | ID:i EQUAL expr:e SEMI_COLON {: RESULT = new Declaration_Assignment(i, e); :};

if ::= IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = new If(e, s); :}
     | IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET ELSE LEFT_CURLY_BRACKET statements:t RIGHT_CURLY_BRACKET {: RESULT = new If(e, s, t); :}
     | IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET elseifs:l {: RESULT = new If(e, s, l, null); :}
     | IF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET elseifs:l ELSE LEFT_CURLY_BRACKET statements:t RIGHT_CURLY_BRACKET {: RESULT = new If(e, s, l, t); :};

elseifs ::= elseifs:f ELSEIF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = f; RESULT.add(new ElseIf(e, s)); :}
          | ELSEIF LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = new LinkedList<ElseIf>(); RESULT.add(new ElseIf(e, s)); :};

cases ::= cases:c CASE INTEGER COLON statements:s {: RESULT = c; RESULT.add(new Case(s)); :}
        | CASE INTEGER COLON statements:s {: RESULT = new LinkedList<Case>(); RESULT.add(new Case(s)); :};

for ::= FOR LEFT_PARENTHESIS INT ID:i EQUAL INTEGER:n SEMI_COLON expr:e SEMI_COLON ID INCREASE RIGHT_PARENTHESIS LEFT_CURLY_BRACKET statements:s RIGHT_CURLY_BRACKET {: RESULT = new For(i, n, e, s); :}
      | FOR LEFT_PARENTHESIS INT ID:i EQUAL INTEGER:n SEMI_COLON expr:e SEMI_COLON ID INCREASE RIGHT_PARENTHESIS LEFT_CURLY_BRACKET RIGHT_CURLY_BRACKET {: RESULT = new For(i, e); :};

expr ::= symbols:s {: RESULT = s; :}
       | aritmetic:a {: RESULT = a; :}
       | logical:l {: RESULT = l; :}
       | unitary:u {: RESULT = u; :}
       | LEFT_PARENTHESIS expr:e RIGHT_PARENTHESIS {: RESULT = new Operation(e); :};

symbols ::= ID:i {:RESULT = new Operation(i, EnumTerminals.ID);:}
          | TEXT:t {:RESULT = new Operation(t, EnumTerminals.TEXT);:}
          | CHARACTER:c {:RESULT = new Operation(c, EnumTerminals.CHARACTER);:};

aritmetic ::= expr:a PLUS expr:b {: RESULT = new Operation(a, EnumOperations.PLUS, b); :}
            | expr:a MINUS expr:b {: RESULT = new Operation(a, EnumOperations.MINUS, b); :}
            | expr:a TIMES expr:b {: RESULT = new Operation(a, EnumOperations.TIMES, b); :}
            | expr:a DIVIDE expr:b {: RESULT = new Operation(a, EnumOperations.DIVIDE, b); :}
            | INTEGER:i {: RESULT = new Operation(i, EnumTerminals.INTEGER); :}
            | DECIMAL:d {: RESULT = new Operation(d, EnumTerminals.DECIMAL); :};

logical ::= expr:a GREATER_THAN expr:b {: RESULT = new Operation(a, EnumOperations.GREATER_THAN, b); :}
          | expr:a LESS_THAN expr:b {: RESULT = new Operation(a, EnumOperations.LESS_THAN, b); :}
          | expr:a GREATER_EQUAL expr:b {: RESULT = new Operation(a, EnumOperations.GREATER_EQUAL, b); :}
          | expr:a LESS_EQUAL expr:b {: RESULT = new Operation(a, EnumOperations.LESS_EQUAL, b); :}
          | expr:a EQUAL_TO expr:b {: RESULT = new Operation(a, EnumOperations.EQUAL_TO, b);:}
          | expr:a NOT_EQUAL_TO expr:b {: RESULT = new Operation(a, EnumOperations.NOT_EQUAL_TO, b);:}
          | expr:a AND expr:b {: RESULT = new Operation(a, EnumOperations.AND, b); :}
          | expr:a OR expr:b {: RESULT = new Operation(a, EnumOperations.OR, b); :}
          | BOOLEAN:b {: RESULT = new Operation(b, EnumTerminals.BOOLEAN); :};

unitary ::= MINUS expr:e {: RESULT = new Operation(e, EnumUnitaryOperations.MINUS); :}
          | NOT expr:e {: RESULT = new Operation(e, EnumUnitaryOperations.NOT); :};